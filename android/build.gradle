group 'io.agora.agora_rtc_engine'
version '1.0-SNAPSHOT'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
  ext.agora_rtc_engine_kt_version = '1.5.31'
  def kotlin_version = rootProject.ext.has('kotlin_version') ? rootProject.ext.get('kotlin_version') : "$agora_rtc_engine_kt_version"

  repositories {
    mavenCentral()
    google()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

rootProject.allprojects {
  repositories {
    mavenCentral()
    google()
  }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion safeExtGet('compileSdkVersion', 31)

  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', 16)

    consumerProguardFiles 'consumer-rules.pro'
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'

    if (isDev(project)) {
      main.jniLibs.srcDirs += 'libs'
    }
  }
}

dependencies {
  if (isDev(project)) {
    api fileTree(dir: "libs", include: ["*.jar", "*.aar"])
  } else {
    api 'com.github.agorabuilder:iris-rtc:3.5.2'
  }

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${safeExtGet('kotlin_version', "$agora_rtc_engine_kt_version")}"
}

static boolean isDev(Project project) {
  def devFile = project.file('.plugin_dev')
  return devFile.exists()
}
