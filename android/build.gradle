group 'io.agora.agora_rtc_engine'
version '1.0-SNAPSHOT'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
  def kotlin_version = rootProject.ext.has('kotlin_version') ? rootProject.ext.get('kotlin_version') : '1.3.72'

  repositories {
    mavenCentral()
    google()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

rootProject.allprojects {
  repositories {
    mavenCentral()
    google()
    maven { url 'https://www.jitpack.io' }
  }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion safeExtGet('compileSdkVersion', 28)
  buildToolsVersion safeExtGet('buildToolsVersion', '28.0.3')

  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', 16)
    targetSdkVersion safeExtGet('targetSdkVersion', 28)

    consumerProguardFiles 'consumer-rules.pro'
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }

  lintOptions {
    disable 'InvalidPackage'
  }

  // During modules merge conflicts have been encountered, they were solved via the following options
  packagingOptions {
    merge "classes.jar"
    merge "AndroidManifest.xml"
    merge "proguard.txt"
    merge "META-INF/com/android/build/gradle/aar-metadata.properties"
    merge "R.txt"
  }
}

dependencies {
  api 'com.github.agorabuilder:native-full-sdk:3.5.2'

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${safeExtGet('kotlin_version', '1.3.72')}"
  // The aar modules must be without DebugInfo else crash on Heap error.
  implementation fileTree(dir: "libs", include: ["*.aar"])
}
